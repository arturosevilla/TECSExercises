// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    /* load logic */
    Mux16(a=muxIncOrOutput, b=in, sel=load, out=muxIncOrLoad);
    Mux16(a=muxIncOrLoad, b=false, sel=reset, out=registerInput);
    Mux16(a=registerOutput, b=incOutput, sel=inc, out=muxIncOrOutput);
    Or(a=reset, b=inc, out=load0);
    Or(a=load0, b=load, out=mustSet);
    
    /* increment logic */
    Inc16(in=registerOutput, out=incOutput);
    
    /* memory */
    Register(in=registerInput, load=mustSet, out=out, out=registerOutput); 
}
