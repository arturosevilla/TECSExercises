// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/03/b/RAM16K.hdl

/**
 * Memory of 16K registers, each 16 bit-wide. Out hold the value
 * stored at the memory location specified by address. If load=1, then 
 * the in value is loaded into the memory location specified by address 
 * (the loaded value will be emitted to out after the next time step.)
 */

CHIP RAM16K {
    IN in[16], load, address[14];
    OUT out[16];

    PARTS:
    DMux4Way(a=load0, b=load1, c=load2, d=load3,
             sel=address[0..1], in=load);
    RAM4K(in=in, load=load0, address=address[2..13], out=out0);
    RAM4K(in=in, load=load1, address=address[2..13], out=out1);
    RAM4K(in=in, load=load2, address=address[2..13], out=out2);
    RAM4K(in=in, load=load3, address=address[2..13], out=out3);
    Mux4Way16(a=out0, b=out1, c=out2, d=out3,
              sel=address[0..1], out=out);
}
